---
title: ROSPlan demonstration, Komodo
---
<!DOCTYPE html>
<html lang="en-us">
	<head>
		<meta charset="UTF-8">
		<title>ROSPlan by KCL-Planning</title>
		<meta name="viewport" content="width=device-width, initial-scale=1">
		<link rel="stylesheet" type="text/css" href="../stylesheets/normalize.css" media="screen">
		<link href='https://fonts.googleapis.com/css?family=Open+Sans:400,700' rel='stylesheet' type='text/css'>
		<link rel="stylesheet" type="text/css" href="../stylesheets/stylesheet.css" media="screen">
		<link rel="stylesheet" type="text/css" href="../stylesheets/github-light.css" media="screen">
		<link rel="stylesheet" type="text/css" href="../stylesheets/syntax.css" media="screen">
		<script src="../jquery.js"></script> 
		<script> 
		$(function(){
		  $("#includedContent").load("../templates/nav-main.html"); 
		  $("#includedSideBar").load("../templates/nav-sidebar-demos.html"); 
		});
		</script>
	</head>
	<body>
    	<div id="includedContent"></div>

		<section class="main-content">

	    	<div id="includedSideBar"></div>
			<section class="documentation-content">

			<h2>SQUIRREL robot</h2>

			<h4>Contents:</h4>
			<ul>
			<li><a href="#v">Video</a></li>
			<li><a href="#d">PDDL domain</a></li>
			</ul>

			<p>This project is funded by the EU FP7 Project SQUIRREL.</p>
			<p>SQUIRREL addresses clutter in an open world by actively controlling the environment, and incrementally learning to extend the robotâ€™s capabilities while doing so.</p>
			<p>You can find more details about this application on the project website:<br>
			<a href="http://www.squirrel-project.eu/">http://www.squirrel-project.eu/</a></p>

			<a id="v"></a>
			<h4>Video</h4>

			<p>The SQUIRREL robot explores by visiting viewcones, discovering and recognising objects.</p>
			<div class="media"><iframe src="https://www.youtube.com/embed/PMI7Yx778gw" frameborder="0" allowfullscreen></iframe></div>

			<a id="d"></a>
			<h4>PDDL domain</h4>

			<p>Shown here are two domains used in the SQUIRREL scenario. The first domain is the most general domain which describes moving between areas, exploring areas, and tidying them. Each of these actions is dispatched from a ROSPlan planning system as a PDDL action. The execution of these actions is carried out by a separate ROSPlan planning system node. Each action (explore, examine, tidy) has a corresponding planning system and domain. Show here is the domain of the exploration action.</p>

			<p>In this way, the problem is heirarchical. The actions of one plan become the goals for the planner in the next level. Using ROSPlan, this behaviour is easy to achieve.</p>

{% highlight lisp linenos=table %}
(define (domain squirrel_tidy_room)
(:requirements :strips :typing :disjunctive-preconditions :negative-preconditions)

(:types
	robot
	area
)

(:predicates
	(explored ?a - area)
	(examined ?a - area)
	(robot_in ?v - robot ?a - area)
	(connected ?from ?to - area)
	(tidy ?a - area)
	(accessible ?from ?to - area)
)

(:action move
	:parameters (?v - robot ?from ?to - area)
	:precondition (and
		(robot_in ?v ?from)
		(accessible ?from ?to)
	)
	:effect (and
		(not (robot_in ?v ?from))
		(robot_in ?v ?to)
	)
)

(:action clear_connection
	:parameters (?v - robot ?from ?to - area)
	:precondition (and
		(robot_in ?v ?from)
		(connected ?from ?to)
	)
	:effect (and
		(accessible ?from ?to)
	)
)

(:action explore_area
	:parameters (?v - robot ?a - area)
	:precondition (and
		(robot_in ?v ?a)
	)
	:effect (and
		(explored ?a)
	)
)

(:action examine_area
	:parameters (?v - robot ?a - area)
	:precondition (and
		(robot_in ?v ?a)
		(explored ?a)
	)
	:effect (and
		(examined ?a)
	)
)

(:action tidy_area
	:parameters (?v - robot ?a - area)
	:precondition (and
		(robot_in ?v ?a)
		(examined ?a)
	)
	:effect (and
		(tidy ?a)
	)
)
)
{% endhighlight %}

{% highlight lisp linenos=table %}
(define (domain squirrel_explore)
(:requirements :strips :typing  :disjunctive-preconditions :negative-preconditions)

(:types
	waypoint
	robot
)

(:predicates
	(explored ?wp - waypoint)
	(robot_at ?v - robot ?wp - waypoint)
)

;; Use perception actions to search for objects at the current waypoint
(:action explore_waypoint
	:parameters (?v - robot ?wp - waypoint)
	:precondition (and
		(robot_at ?v ?wp)
	)
	:effect (and
		(explored ?wp)
	)
)

;; Move between any two waypoints, avoiding terrain
(:action goto_waypoint
	:parameters (?v - robot ?from ?to - waypoint)
	:precondition (and
		(robot_at ?v ?from))
	:effect (and
		(not (robot_at ?v ?from))
		(robot_at ?v ?to)
	)
)
)
{% endhighlight %}

			</section>

			<footer class="site-footer">
				<span class="site-footer-owner"><a href="https://github.com/KCL-Planning/ROSPlan">ROSPlan</a> is maintained by <a href="https://github.com/KCL-Planning">KCL-Planning</a>.</span>
				<span class="site-footer-credits">This page was generated by <a href="https://pages.github.com">GitHub Pages</a> using the <a href="https://github.com/jasonlong/cayman-theme">Cayman theme</a> by <a href="https://twitter.com/jasonlong">Jason Long</a>.</span>
			</footer>

		</section>


	</body>
</html>
