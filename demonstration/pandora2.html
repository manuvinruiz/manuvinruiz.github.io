---
title: ROSPlan demonstration, Komodo
---
<!DOCTYPE html>
<html lang="en-us">
	<head>
		<meta charset="UTF-8">
		<title>ROSPlan by KCL-Planning</title>
		<meta name="viewport" content="width=device-width, initial-scale=1">
		<link rel="stylesheet" type="text/css" href="../stylesheets/normalize.css" media="screen">
		<link href='https://fonts.googleapis.com/css?family=Open+Sans:400,700' rel='stylesheet' type='text/css'>
		<link rel="stylesheet" type="text/css" href="../stylesheets/stylesheet.css" media="screen">
		<link rel="stylesheet" type="text/css" href="../stylesheets/github-light.css" media="screen">
		<link rel="stylesheet" type="text/css" href="../stylesheets/syntax.css" media="screen">
		<script src="../jquery.js"></script> 
		<script> 
		$(function(){
		  $("#includedContent").load("../templates/nav-main.html"); 
		  $("#includedSideBar").load("../templates/nav-sidebar-demos.html"); 
		});
		</script>
	</head>
	<body>
    	<div id="includedContent"></div>

		<section class="main-content">

	    	<div id="includedSideBar"></div>
			<section class="documentation-content">

			<h2>Girona 500 AUV Valve Turning</h2>

			<h4>Contents:</h4>
			<ul>
			<li><a href="#v">Video</a></li>
			<li><a href="#d">PDDL domain</a></li>
			</ul>

			<p>This project was funded by the EU FP7 Project PANDORA.</p>
			<p>University of Girona, King's College London, Italian Institute of Technology, and the National Technical University of Athens combine visual-based detection and localization systems, robust control schemes, learning-by-demonstration techniques for intervention, and temporal planning. The Girona 500 AUV operates a valve panel many times in several disturbances: water currents, blocked valves, unknown panel position, and panel occlusion. The positive results encourage the use of, in the short-term, autonomous robots operating in subsea facilities performing interventions with a cost benefit, in comparison with teleoperated vehicles.</p>

			<p>The long-term mission was carried out in a water tank of 16x8x5 meters using the Girona 500 AUV equipped with a 4-Degrees of Freedom electrical manipulator, a Stereo Camera and a specifically designed end-effector with an in-hand camera and force-torque sensor. To make the environment more realistic, two propellers, able to generate up to 14Kg of thrust each, were placed close to the panel in order to generate lateral water currents. Experiments were performed in a completely autonomous mode, the vehicle ran on its own batteries and all required processing was performed with the on-board computers.</p>

In the experiment, the vehicle had to locate the intervention panel among different locations and modify the valve handles to achieve different panel configurations. During more than 3 hours, the AUV was required changed the panel to 9 different configurations in various time windows, which required the turning of 29 valves.</p>

			<p>You can find more details about this application in the following papers:<br><br>

			<em>ROSPlan: Planning in the Robot Operating System</em><br>
			M. Cashmore, M. Fox, D. Long, D. Magazzeni, B. Ridder, A. Carrera, N. Palomeras, N. Hurtos, M. Carreras.<br>
			Proceedings of the 25th International Conference on Automated Planning and Scheduling (ICAPS-15). June 2015.</p>

			<p><em>Toward persistent autonomous intervention in a subsea panel</em><br>
			N. Palomeras, A. Carrera, N. Hurts, G. C. Karras, C. P. Bechlioulis, M. Cashmore, D. Magazzeni, D. Long, M. Fox, K. J. Kyriakopoulos, P. Kormushev, J. Salvi and M. Carreras<br>
			Autonomous Robots, 2015</p>

			<a id="v"></a>
			<h4>Video</h4>

			<p>The Girona 500 AUV from the University of Girona <a href="http://cirs.udg.edu/">Underwater Vision and Robotics Team</a> searches for and examines valve panels, and turns valves within time windows.</p>
			<div class="media"><iframe src="https://www.youtube.com/embed/klHBFiwgOZY" frameborder="0" allowfullscreen></iframe></div>

			<a id="d"></a>
			<h4>PDDL domain</h4>

{% highlight lisp linenos=table %}
(define (domain pandora-domain-persistent)
(:requirements :strips :typing :fluents :disjunctive-preconditions :durative-actions)

(:types
		waypoint 
		inspectionpoint
		panel
		valve
		vehicle)

(:predicates
	(connected ?wp1 ?wp2 - waypoint)
	(at ?v - vehicle ?wp - waypoint)
	(near ?v - vehicle ?wp - waypoint)
	(cansee ?v - vehicle ?wp - waypoint ?ip - inspectionpoint)
	(canexamine ?v - vehicle ?p - panel ?wp - waypoint)
	(canreach ?v - vehicle ?p - panel ?wp - waypoint)
	(examined ?p - panel)
	(on ?a - valve ?p - panel)
	(turned ?a - valve)
	(chainat ?wp - waypoint)
)

(:functions
		(observed ?ip - inspectionpoint)
		(obs ?ip - inspectionpoint ?wp - waypoint)
		(distance ?wp1 ?wp2 - waypoint) 
)

(:durative-action do_hover
	:parameters (?v - vehicle ?from ?to - waypoint)
	:duration ( = ?duration (* (distance ?from ?to) 10))
	:condition (and (at start (at ?v ?from)) (at start (connected ?from ?to)))
	:effect (and (at start (not (at ?v ?from))) (at end (at ?v ?to)))
)

(:durative-action do_hover_fast
	:parameters (?v - vehicle ?from ?to - waypoint)
	:duration ( = ?duration (* (distance ?from ?to) 5))
	:condition (and (at start (at ?v ?from)) (at start (connected ?from ?to)))
	:effect (and (at start (not (at ?v ?from))) (at end (near ?v ?to)))
)

(:durative-action correct_position
	:parameters (?v - vehicle ?target - waypoint)
	:duration ( = ?duration 10)
	:condition (at start (near ?v ?target))
	:effect (and (at start (not (near ?v ?target))) (at end (at ?v ?target)))
)

(:durative-action valve_state
	:parameters (?v - vehicle ?wp - waypoint ?p - panel)
	:duration ( = ?duration 10)
	:condition (and (at start (at ?v ?wp)) (at start (canexamine ?v ?p ?wp)))
	:effect (and (at start (not (canexamine ?v ?p ?wp))) (at end (examined ?p)))
)

(:durative-action observe
	:parameters (?v - vehicle ?wp - waypoint ?ip - inspectionpoint)
	:duration ( = ?duration 10)
	:condition (and (at start (at ?v ?wp)) (at start (cansee ?v ?wp ?ip)))
	:effect (and 
			(and (at start (not (cansee ?v ?wp ?ip))) (at end (increase (observed ?ip) (obs ?ip ?wp))))
			(and (at start (not (at ?v ?wp))) (at end (near ?v ?wp))))
)

(:durative-action turn_valve
	:parameters (?v - vehicle ?wp - waypoint ?p - panel ?a - valve)
	:duration ( = ?duration 30)
	:condition (and (and (at start (at ?v ?wp)) (at start (canreach ?v ?p ?wp))) (at start (on ?a ?p)))
	:effect (and (at end (turned ?a)) (and (at start (not (at ?v ?wp))) (at end (near ?v ?wp))))
)

(:durative-action follow_chain
	:parameters (?v - vehicle ?from - waypoint)
	:duration ( = ?duration 120)
	:condition (and (at start (at ?v ?from)) (at start (chainat ?from)))
	:effect (and (at start (not (at ?v ?from))) (at end (near ?v ?from)))
)
)
{% endhighlight %}
			</section>

			<footer class="site-footer">
				<span class="site-footer-owner"><a href="https://github.com/KCL-Planning/ROSPlan">ROSPlan</a> is maintained by <a href="https://github.com/KCL-Planning">KCL-Planning</a>.</span>
				<span class="site-footer-credits">This page was generated by <a href="https://pages.github.com">GitHub Pages</a> using the <a href="https://github.com/jasonlong/cayman-theme">Cayman theme</a> by <a href="https://twitter.com/jasonlong">Jason Long</a>.</span>
			</footer>

		</section>


	</body>
</html>
